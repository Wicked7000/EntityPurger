buildscript {
    repositories {
        jcenter()
        maven {
            name = "forge"
            url = "https://maven.minecraftforge.net/"
        }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: "com.github.johnrengelman.shadow"

version = "1.2.4"
group = "com.wicked.entitypurger"
archivesBaseName = "entitypurger"

sourceCompatibility = targetCompatibility = '1.8'
compileJava {
    sourceCompatibility = targetCompatibility = '1.8'
}
sourceSets.main.resources { srcDir 'src/generated/resources' }

minecraft {
    mappings channel: mappings_channel, version: mappings_version
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"
    shadow group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: '2.11.0'
    shadow group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    compileOnly fileTree(include: ['*.jar'], dir: 'libs')
}

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", minecraft_version

    filesMatching('mcmod.info') {
        expand 'version': project.version, 'mcversion': minecraft_version
    }
}

shadowJar {
    configurations = [project.configurations.shadow]
    exclude "module-info.class" //To not cause issues with Java9 built modules

    relocate "com.fasterxml.jackson.core", "com.wicked.fasterxml.jackson.core"
    relocate "com.fasterxml.jackson.databind", "com.wicked.fasterxml.jackson.databind"
    relocate "com.fasterxml.jackson.annotation", "com.wicked.fasterxml.jackson.annotation"

    Jar jarConfig = project.getTasks().getByName("jar")
    println(jarConfig.getArchiveName())
    archiveName(jarConfig.getArchiveName())
}

jar {
    manifest {
        attributes([
                "Specification-Title"     : mod_name,
                "Specification-Version"   : "1",
                "Implementation-Title"    : mod_name,
                "Implementation-Version"  : project.version,
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

jar.dependsOn(shadowJar)
jar.finalizedBy('reobfJar')
jar.enabled = false;

